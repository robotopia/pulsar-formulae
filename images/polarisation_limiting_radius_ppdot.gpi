reset

set logscale xy
set xlabel "$P$ (s)"
set ylabel "$\\dot{P}$"
set format xy "10^{%L}"
unset key

Pdot_min = 1e-22
#set xrange [1e-3:2e2]
#set yrange [Pdot_min:5e-10]
set trange [Pdot_min:5e-10]

set parametric
set dummy Pdot

set label 1 "III" at graph 0.1,0.9 font "Times,24"

II_III(Pdot) = 0.034*(Pdot/1e-15)**(1./5.) # Returns P in seconds

# For a given r_pl in region II, the place where the contour meets the II-III boundary is at a Pdot15 (= Pdot/10^15) of
#  r_pl/r_p = 873 P^(1/2) Pdot15^(1/10)
#           = 873 (0.034 Pdot15^(1/5))^(1/2) Pdot15^(1/10)
#           = 161.4 Pdot15^(1/5)
# Pdot15 = 0.362 (r_pl/r_p)^5

# The first line of the above, rearranged:
# P = 1.31e-6 (r_pl/r_p)^2 Pdot15^(-1/5)

# Half beam opening angle
xi = 10*pi/180.0

IIB_P(rpl_rp, Pdot) = 1.31e-6*(rpl_rp)**2 * (Pdot/1e-15)**(-1./5.)
IIB_max_Pdot(rpl_rp) = 9.13e-12*(rpl_rp)**5 # Fix me

do for [r10=1:3] {
    set label r10+2 "r_{pl}/r_p = 10^".r10 at IIB_P(10**r10, 1e-21), 1e-21 tc rgb "#c0c0c0" center
}

plot '< psrcat -x -c "p0 p1"' u 1:4 pt 7 ps 0.25, \
     II_III(Pdot), Pdot lc rgb "black", \
     for [r10=1:3] [Pdot_min:IIB_max_Pdot(10**r10)] IIB_P(10**r10, Pdot), Pdot lc rgb "#c0c0c0", \
     for [r10=1:3] IIB_P(10**r10, IIB_max_Pdot(10**r10)), IIB_max_Pdot(10**r10) w p lc rgb "red"
